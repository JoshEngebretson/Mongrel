//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id: Inventory.vc 4327 2010-07-24 19:30:53Z firebrand_kh $
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************
//**
//**	Base class for all items that a player can carry.
//**
//**************************************************************************

class Inventory : Actor
	abstract;

const float BONUSADD		= 0.2;

const int DefMaxAmount		= -2;

//	Special values for powerup's blend colour.
const int INVERSECOLOUR		= 0x00123456;
const int GOLDCOLOUR		= 0x00123457;
const int REDCOLOUR			= 0x00123458;
const int GREENCOLOUR		= 0x00123459;

int					Amount;					// Amount of item this instance has.
int					InterHubAmount;			// Amount of item that can be kept between hubs or levels.
int					MaxAmount;				// Max amount of item this instance can have.
string				PickupMessage;
float				RespawnTime;			// Tics from pickup time to respawn time.
int					GiveQuestNum;
name				IconName;
float				DropTime;
//	Spawn PickuFlash when picked up.
class<Actor>		PickupFlashType;

name				PickupSound;
name				UseSound;

//	Originally these didn't respawn in deathmatch
bool				bBigPowerup;
//	Play pickup sound at full volume
bool				bFullVolPickupSound;
//	Used by HandlePickup.
bool				bPickupGood;
//	Can be displayed in the inventory bar
bool				bInvBar;
//	Keep item in inventory even if amount is 0
bool				bKeepDepleted;
//	This item cannot be tossed out
bool				bUndroppable;
//	Ignore skill level when giving this item
bool				bIgnoreSkill;
//	Automaticly use when picked up.
bool				bAutoActivate;
//	For auto activate items always pick up the item, even if it wasn't used.
bool				bAlwaysPickup;
//	Keep powerup while travelingbetween levels in a hub
bool				bHubPower;
//	Remove when traveling between different clusters.
bool				bInterHubStrip;
//	No feedback when picked up
bool				bQuiet;
//	When picked up while it's still active, time is added
bool				bAdditiveTime;
//	CreateCopy changed the owner
bool				bCreateCopyMoved;
//	Powerup failed to activate.
bool				bInitEffectFailed;

replication
{
	reliable if (Role == ROLE_Authority && bNetOwner)
		Amount, MaxAmount;
}

//==========================================================================
//
//	OnMapSpawn
//
//==========================================================================

void OnMapSpawn(mthing_t* mthing)
{
	if (!mthing)
	{
		//	Non-placed items are dropped by default.
		bDropped = true;
	}
	if (MaxAmount == DefMaxAmount)
	{
		MaxAmount = LineSpecialGameInfo(Level.Game).InvDefaultMaxAmount;
	}
	::OnMapSpawn(mthing);
}

//==========================================================================
//
//	Destroyed
//
//==========================================================================

void Destroyed()
{
	if (Role == ROLE_Authority)
	{
		if (Owner)
		{
			EntityEx(Owner).RemoveInventory(self);
		}
		Inventory = none;
	}
	::Destroyed();
}

//==========================================================================
//
//	Tick
//
//==========================================================================

void Tick(float DeltaTime)
{
	::Tick(DeltaTime);
	if (DropTime)
	{
		DropTime -= DeltaTime;
		if (DropTime <= 0.0)
		{
			DropTime = 0.0;
			bSpecial = default.bSpecial;
			bSolid = default.bSolid;
		}
	}
}

//==========================================================================
//
//	BecomeItem
//
//==========================================================================

final void BecomeItem()
{
	UnlinkFromWorld();
	bNoSector = true;
	bNoBlockmap = true;
	LinkToWorld();
	SetTID(0);
	bSpecial = false;
	SetState(FindState('Held'));
}

//==========================================================================
//
//	BecomePickup
//
//==========================================================================

final void BecomePickup()
{
	if (Owner)
	{
		EntityEx(Owner).RemoveInventory(self);
	}
	UnlinkFromWorld();
	bNoSector = false;
	bNoBlockmap = false;
	LinkToWorld();
	bSpecial = default.bSpecial;
	SetState(IdleState);
}

//==========================================================================
//
//	AttachToOwner
//
//==========================================================================

void AttachToOwner(EntityEx NewOwner)
{
	BecomeItem();
	NewOwner.AddInventory(self);
}

//==========================================================================
//
//	DetachedFromOwner
//
//	Event called when item is being removed from Owner's inventory.
//
//==========================================================================

void DetachedFromOwner()
{
}

//==========================================================================
//
//	TouchSpecial
//
//==========================================================================

final void TouchSpecial(EntityEx Toucher)
{
	if (!Toucher.bPickUp)
	{
		// Can't be picked up by toucher
		return;
	}
	if (Toucher.Health <= 0)
	{
		// Toucher is dead
		return;
	}

	//	Make sure that if it's a voodoo doll touching item it's given to the
	// actual player actor.
	if (Toucher.bIsPlayer)
	{
		Toucher = EntityEx(Toucher.Player.MO);
	}

	if (!TryPickup(Toucher))
	{
		return;
	}

	if (!bQuiet)
	{
		if (Toucher.bIsPlayer)
		{
			Toucher.Player.cprint(GetPickupMessage());
		}
		PlayPickupSound(Toucher);
		if (Toucher.bIsPlayer)
		{
			PlayerEx(Toucher.Player).BonusFlash += BONUSADD;
		}
	}

	DoPickupSpecial(Toucher);
	if (bCountItem)
	{
		if (Toucher.bIsPlayer)
		{
			Toucher.Player.ItemCount++;
		}
		Level.CurrentItems++;
	}
}

//==========================================================================
//
//	TryPickup
//
//==========================================================================

bool TryPickup(EntityEx Toucher)
{
	EntityEx NewToucher = Toucher;
	bPickupGood = false;
	if (Toucher.Inventory && Toucher.Inventory.HandlePickup(self))
	{
		if (!bPickupGood)
		{
			return false;
		}
		GoAwayAndDie();
	}
	else if (MaxAmount == 0)
	{
		//	A special case: Items with max amount of 0 can be picked up if
		// they are autoactivate-able.
		if (!bAutoActivate)
		{
			return false;
		}

		//	Put it in the inventory long enoung to be used.
		Toucher.AddInventory(self);
		bool UseGood = Use(true);
		Toucher.RemoveInventory(self);

		if (UseGood || bAlwaysPickup)
		{
			GoAwayAndDie();
		}
		else
		{
			return false;
		}
	}
	else
	{
		Inventory Copy = CreateCopy(Toucher);
		if (!Copy)
		{
			return false;
		}
		//	Some powerups cannot activate absolutely, for example,
		// PowerMorph; fail the pickup if so.
		if (Copy.bInitEffectFailed)
		{
			if (Copy != self)
			{
				Copy.Destroy();
			}
			else
			{
				bInitEffectFailed = false;
			}
			return false;
		}
		//	Handle owner-changing powerups
		if (Copy.bCreateCopyMoved)
		{
			NewToucher = EntityEx(Copy.Owner);
			Copy.Owner = none;
			Copy.bCreateCopyMoved = false;
		}
		Copy.AttachToOwner(NewToucher);
		if (bAutoActivate && Copy.Use(true))
		{
			Copy.Amount--;
			if (Copy.Amount <= 0)
			{
				Copy.bSpecial = false;
				Copy.SetState(Copy.FindState('HoldAndDestroy'));
			}
		}
	}
	GiveQuest(Toucher);
	return true;
}

//==========================================================================
//
//	HandlePickup
//
//	Called for each item in touchers inventory. Returns true if pickup
// should be interrupted. Sets bPickupGood to true if pickup should be
// considered successful.
//
//==========================================================================

bool HandlePickup(Inventory Item)
{
	if (Item.Class == Class)
	{
		if (Amount < MaxAmount)
		{
			Amount += Item.Amount;
			if (Amount > MaxAmount)
			{
				Amount = MaxAmount;
			}
			Item.bPickupGood = true;
		}
		return true;
	}

	if (Inventory)
	{
		return Inventory.HandlePickup(Item);
	}
	return false;
}

//==========================================================================
//
//	CreateCopy
//
//==========================================================================

Inventory CreateCopy(EntityEx Toucher)
{
	Inventory Copy;
	if (GoAway())
	{
		//	Must create a copy
		Copy = Spawn(class<Inventory>(Class),,,, false);
		Copy.Amount = Amount;
		Copy.MaxAmount = MaxAmount;
	}
	else
	{
		//	Use this one
		Copy = self;
	}
	return Copy;
}

//==========================================================================
//
//	CreateTossable
//
//	Create item suitable fro dropping.
//
//==========================================================================

Inventory CreateTossable()
{
	if (!IdleState)
	{
		//	This item has no spawn state so it cannot be seen.
		return none;
	}
	if (bUndroppable || !Owner || Amount <= 0)
	{
		return none;
	}

	if (Amount == 1 && !bKeepDepleted)
	{
		//	Drop the current one.
		BecomePickup();
		DropTime = 1.0;
		bSpecial = false;
		bSolid = false;
		return self;
	}

	Inventory Copy = Spawn(class<Inventory>(Class),,,, false);
	Copy.Amount = 1;
	Copy.MaxAmount = MaxAmount;
	Copy.DropTime = 1.0;
	Copy.bSpecial = false;
	Copy.bSolid = false;
	Amount--;
	return Copy;
}

//==========================================================================
//
//	ShouldStay
//
//	Returns true if item should stay after it's been picked up.
//
//==========================================================================

bool ShouldStay()
{
	return false;
}

//==========================================================================
//
//	ShouldRespawn
//
//==========================================================================

bool ShouldRespawn()
{
	if (bBigPowerup && !LineSpecialGameInfo(Level.Game).bRespawnBigItems)
	{
		return false;
	}
	return LineSpecialGameInfo(Level.Game).bRespawnItems;
}

//==========================================================================
//
//	GoAway
//
//==========================================================================

final bool GoAway()
{
	//	Never respawn dropped items.
	if (bDropped)
	{
		if (PickupFlashType)
		{
			//  Initiate the artifact pickup animation.
			Spawn(PickupFlashType, Origin);
		}
		return false;
	}

	if (!ShouldStay())
	{
		if (PickupFlashType)
		{
			//  Initiate the artifact pickup animation.
			Spawn(PickupFlashType, Origin);
		}
		if (ShouldRespawn())
		{
			Hide();
		}
		else
		{
			// Don't respawn
			return false;
		}
	}
	return true;
}

//==========================================================================
//
//	GoAwayAndDie
//
//==========================================================================

final void GoAwayAndDie()
{
	if (!GoAway())
	{
		bSpecial = false;
		SetState(FindState('HoldAndDestroy'));
	}
}

//==========================================================================
//
//	Hide
//
//==========================================================================

final void Hide()
{
	bSpecial = false;
	bInvisible = true;
	if (LineSpecialGameInfo(Level.Game).bRavenStylePickupRespawn)
	{
		SetState(FindState('HideSpecial'));
		StateTime = 40.0;
		if (PickupFlashType)
		{
			StateTime += 30.0 / 35.0;
		}
	}
	else
	{
		SetState(FindState('DormantPickup'));
		StateTime = 30.0;
	}
	if (RespawnTime)
	{
		StateTime = RespawnTime;
	}
}

//==========================================================================
//
//	GetPickupMessage
//
//==========================================================================

string GetPickupMessage()
{
	return PickupMessage;
}

//==========================================================================
//
//	DoPickupSpecial
//
//==========================================================================

void DoPickupSpecial(EntityEx Toucher)
{
	if (Special)
	{
		Level.ExecuteActionSpecial(Special, Args[0], Args[1], Args[2],
			Args[3], Args[4], NULL, 0, Toucher);
		Special = 0;
	}
}

//==========================================================================
//
//	PlayPickupSound
//
//==========================================================================

void PlayPickupSound(EntityEx Toucher)
{
	if (PickupSound)
	{
		Toucher.PlaySound(PickupSound, CHAN_BODY, 1.0,
			bFullVolPickupSound ? ATTN_NONE : ATTN_NORMAL);
	}
}

//==========================================================================
//
//	GiveQuest
//
//==========================================================================

final void GiveQuest(EntityEx Toucher)
{
	if (GiveQuestNum)
	{
		Toucher.GiveInventoryType(class<Inventory>(FindClass(
			StrToName(va("QuestItem%d", GiveQuestNum)))));
	}
}

//==========================================================================
//
//  DoRespawn
//
//==========================================================================

bool DoRespawn()
{
	return true;
}

//==========================================================================
//
//	SpecialDropAction
//
//==========================================================================

bool SpecialDropAction(EntityEx Source)
{
	return false;
}

//==========================================================================
//
//	PrevInv
//
//	Return previous item with bInvBar flag set.
//
//==========================================================================

Inventory PrevInv()
{
	Inventory Item = Inventory;
	while (Item && !Item.bInvBar)
	{
		Item = Item.Inventory;
	}
	return Item;
}

//==========================================================================
//
//	NextInv
//
//	Return next item with bInvBar flag set.
//
//==========================================================================

Inventory NextInv()
{
	Inventory Ret = none;
	Inventory Item = EntityEx(Owner).Inventory;
	while (Item && Item != self)
	{
		if (Item.bInvBar)
		{
			Ret = Item;
		}
		Item = Item.Inventory;
	}
	return Ret;
}

//==========================================================================
//
//	Use
//
//==========================================================================

bool Use(bool Pickup)
{
	return false;
}

//==========================================================================
//
//	DoEffect
//
//==========================================================================

void DoEffect()
{
}

//==========================================================================
//
//	GetBlend
//
//==========================================================================

int GetBlend()
{
	return 0;
}

//==========================================================================
//
//	AbsorbDamage
//
//==========================================================================

void AbsorbDamage(int damage, name DmgType, out int NewDamage)
{
	if (Inventory)
	{
		Inventory.AbsorbDamage(damage, DmgType, NewDamage);
	}
}

//==========================================================================
//
//	OwnerDied
//
//==========================================================================

void OwnerDied()
{
}

//==========================================================================
//
//	GetSpeedFactor
//
//==========================================================================

float GetSpeedFactor()
{
	if (Inventory)
	{
		return Inventory.GetSpeedFactor();
	}
	else
	{
		return 1.0;
	}
}

//**************************************************************************
//	State jumps
//**************************************************************************

//==========================================================================
//
//  A_JumpIfNoAmmo
//
//==========================================================================

final void A_JumpIfNoAmmo(state Label)
{
	//	State jumps should not affect state chain results.
	if (XLevel.StateCall)
	{
		XLevel.StateCall->Result = false;
	}

	//	Only for weapons.
	if (!bIsPlayer || XLevel.StateCall)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;
	if (!Wpn)
	{
		return;
	}

	if (!Wpn.CheckAmmo(Wpn.FireMode, false, true))
	{
		DoJump(Label);
	}
}

//**************************************************************************
//	Weapon functions
//**************************************************************************

//==========================================================================
//
//  A_Lower
//
//  Lowers current weapon, and changes weapon at bottom.
//
//==========================================================================

final void A_Lower()
{
	if (!bIsPlayer)
	{
		return;
	}

	if (PlayerEx(Player).MorphTime ||
		PlayerEx(Player).bInstantWeaponSwitch)
	{
		Player.ViewStates[ps_weapon].SY = Weapon::WEAPONBOTTOM;
	}
	else
	{
		//	FIXME!!
		if (Level.Game.frametime < 1.0 / 35.0)
		{
			Player.ViewStates[ps_weapon].SY += Weapon::LOWERSPEED;
		}
		else
		{
			Player.ViewStates[ps_weapon].SY += Weapon::LOWERSPEED * 35.0 *
				Level.Game.frametime;
		}
	}

	if (Player.ViewStates[ps_weapon].SY < Weapon::WEAPONBOTTOM)
	{
		// Not lowered all the way yet
		return;
	}

	if (Player.PlayerState == PST_DEAD)
	{
		// Player is dead, so don't bring up a pending weapon
		Player.ViewStates[ps_weapon].SY = Weapon::WEAPONBOTTOM;
		return;
	}

	if (Player.MO.Health <= 0)
	{
		// Player is dead, so keep the weapon off screen.
		Player.SetViewState(ps_weapon, none);
		return;
	}

	//	Clear flash state, needed for Strife.
	Player.SetViewState(ps_flash, none);

	PlayerEx(Player).SetWeapon(PlayerEx(Player).PendingWeapon);
	PlayerEx(Player).BringUpWeapon();
}

//==========================================================================
//
//  A_Raise
//
//==========================================================================

final void A_Raise()
{
	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	//	FIXME!!
	if (Level.Game.frametime < 1.0 / 35.0)
	{
		Player.ViewStates[ps_weapon].SY -= Weapon::RAISESPEED;
	}
	else
	{
		Player.ViewStates[ps_weapon].SY -= Weapon::RAISESPEED * 35.0 *
			Level.Game.frametime;
	}
	if (Player.ViewStates[ps_weapon].SY > Weapon::WEAPONTOP)
	{
		// Not raised all the way yet
		return;
	}
	Player.ViewStates[ps_weapon].SY = Weapon::WEAPONTOP;
	if (Wpn)
	{
		Player.SetViewState(ps_weapon, Wpn.GetReadyState());
	}
	else
	{
		Player.SetViewState(ps_weapon, none);
	}
}

//===========================================================================
//
//  A_ReFire
//
//  The player can re-fire the weapon without lowering it entirely.
//
//===========================================================================

final void A_ReFire()
{
	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	if (Wpn.FireMode == Weapon::FIRE_Primary &&
		(Player.Buttons & BT_ATTACK) &&
		!PlayerEx(Player).PendingWeapon && Player.MO.Health > 0)
	{
		PlayerEx(Player).Refire++;
		PlayerEx(Player).FireWeapon();
	}
	else if (Wpn.FireMode == Weapon::FIRE_Secondary &&
		(Player.Buttons & BT_ALT_ATTACK) &&
		!PlayerEx(Player).PendingWeapon && Player.MO.Health > 0)
	{
		PlayerEx(Player).Refire++;
		PlayerEx(Player).AltFireWeapon();
	}
	else
	{
		PlayerEx(Player).Refire = 0;
		Wpn.CheckAmmo(Wpn.FireMode, true);
	}
}

//===========================================================================
//
//  A_ClearReFire
//
//===========================================================================

final void A_ClearReFire()
{
	if (bIsPlayer)
	{
		PlayerEx(Player).Refire = 0;
	}
}

//===========================================================================
//
//  A_GunFlash
//
//===========================================================================

final void A_GunFlash()
{
	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	SetState(MeleeState);
	Player.SetViewState(ps_flash, Wpn.FindState('Flash'));
}

//===========================================================================
//
//  A_CheckReload
//
//===========================================================================

final void A_CheckReload()
{
	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	Wpn.CheckAmmo(Wpn.FireMode, true);
}

//===========================================================================
//
//  A_Light
//
//===========================================================================

final void A_Light(int LightValue)
{
	if (!bIsPlayer)
	{
		return;
	}

	if (LightValue < 0)
	{
		LightValue = 0;
	}
	else if (LightValue > 20)
	{
		LightValue = 20;
	}
	Player.ExtraLight = LightValue;
}

//===========================================================================
//
//  A_Light0
//
//===========================================================================

final void A_Light0()
{
	if (!bIsPlayer)
	{
		return;
	}

	Player.ExtraLight = 0;
}

//===========================================================================
//
//  A_Light1
//
//===========================================================================

final void A_Light1()
{
	if (!bIsPlayer)
	{
		return;
	}

	Player.ExtraLight = 1;
}

//===========================================================================
//
//  A_Light2
//
//===========================================================================

final void A_Light2()
{
	if (!bIsPlayer)
	{
		return;
	}

	Player.ExtraLight = 2;
}

//==========================================================================
//
//	A_LightInverse
//
//==========================================================================

final void A_LightInverse()
{
	if (bIsPlayer)
	{
		Player.ExtraLight = 255;
	}
}



//==========================================================================
//
//  A_RestoreSpecialThing1
//
//  Make a special thing visible again.
//
//==========================================================================

final void A_RestoreSpecialThing1()
{
	bInvisible = false;
	if (DoRespawn())
	{
		PlaySound('misc/spawn', CHAN_VOICE);
	}
}

//==========================================================================
//
//  A_RestoreSpecialThing2
//
//==========================================================================

final void A_RestoreSpecialThing2()
{
	bSpecial = true;
	SetState(IdleState);
}

states
{
	//	Hide pickup like in Doom and Strife.
DormantPickup:
	TNT1 A 1050
	TNT1 A -1
	{
		EntityEx	A;

		bInvisible = false;
		bSpecial = true;
		SetState(IdleState);

		// spawn a teleport fog at the new spot
		//A = Spawn(ItemFog, Origin);
		//A.PlaySound('misc/spawn', CHAN_ITEM);
	}
	Stop

	//	Hide for 40 secs
HideSpecial:
	ACLO E 1400
	ACLO A 4 A_RestoreSpecialThing1
	ACLO BABCBCDC 4
	ACLO D 4 A_RestoreSpecialThing2
	Stop

	//	Invisible state for held items
Held:
	TNT1 A -1
	Stop

	//	Will destroy item as soon as possible
HoldAndDestroy:
	TNT1 A 1
	Stop
}

defaultproperties
{
	Amount = 1;
	InterHubAmount = 1;
	MaxAmount = 1;
	bSpecial = true;
	PickupMessage = "You got a pickup";
	PickupSound = 'misc/i_pkup';
	UseSound = 'misc/invuse';
}
